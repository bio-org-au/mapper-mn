plugins {
    id "groovy"
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "application"
}


version "2.0"
group "mapper.mn"

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly
}

dependencies {
    annotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    annotationProcessor "io.micronaut:micronaut-security"
    compileOnly "io.micronaut.configuration:micronaut-openapi"
    compileOnly platform("io.micronaut:micronaut-bom:$micronautVersion")
    compileOnly "io.micronaut:micronaut-inject-groovy"
    implementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    implementation "io.micronaut:micronaut-runtime-groovy"
    implementation "io.micronaut:micronaut-validation"
    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.swagger.core.v3:swagger-annotations"
    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"

    compile "io.micronaut:micronaut-security-jwt"
    
    compile 'org.codehaus.groovy:groovy-sql:2.5.2'
    runtime 'io.micronaut.configuration:micronaut-jdbc-hikari'
    runtime 'org.postgresql:postgresql:42.2.1'

    testCompileOnly platform("io.micronaut:micronaut-bom:$micronautVersion")
    testImplementation("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "io.micronaut.test:micronaut-test-spock"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.5.0"
}

test.classpath += configurations.developmentOnly

mainClassName = "au.org.biodiversity.mapper.Application"
tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
}

shadowJar {
    mergeServiceFiles()
}

run.classpath += configurations.developmentOnly
run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote',
        "-Dmicronaut.config.files=${System.getProperty("user.home")}/.nsl/nsl-mapper-config-mn.groovy")

test.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote',
        "-Dmicronaut.config.files=./src/test/resource/nsl-mapper-config-mn.groovy")

task createTestDb(type: Exec) {
    workingDir "$projectDir/src/test/resource"
    commandLine './createTestDb.sh'
}

tasks.withType(Test) {
    dependsOn(createTestDb)
}

task dockerPublish() {
    dependsOn(assemble)
    doFirst {
        exec {
            workingDir(projectDir)
            ignoreExitValue(true)
            commandLine 'docker', 'image', 'rm', 'nsl-mapper'
        }
    }
    doLast {
        exec {
            workingDir(projectDir)
            commandLine 'docker', 'build', '--tag=nsl-mapper', '.'
        }
        exec {
            workingDir(projectDir)
            commandLine "docker", "tag", "nsl-mapper", "pmcneil/nsl-mapper:${version}"
        }
        exec {
            workingDir(projectDir)
            commandLine "docker", "push", "pmcneil/nsl-mapper:${version}"
        }
    }
}